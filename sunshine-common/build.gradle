plugins {
	id 'java'
	id 'maven-publish'
	id 'signing'
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'


dependencies {
	compile project(":sunshine-base")
	implementation group: 'org.thymeleaf', name: 'thymeleaf', version: '3.0.11.RELEASE'
	implementation group: 'ognl', name: 'ognl', version: '3.2.10'
	implementation group: 'org.apache.poi', name: 'poi', version: '4.1.2'
	implementation group: 'org.apache.poi', name: 'poi-ooxml', version: '4.1.2'
	implementation group: 'io.kubernetes', name: 'client-java', version: '7.0.0'
	implementation group: 'org.yaml', name: 'snakeyaml', version: '1.25'
	implementation group: 'org.jsoup', name: 'jsoup', version: '1.12.1'
	implementation (group: 'com.ctrip.framework.apollo', name: 'apollo-client', version: '1.3.0')
	implementation group: 'org.mybatis', name: 'mybatis', version: '3.5.4'
	implementation group: 'io.lettuce', name: 'lettuce-core', version: '5.2.2.RELEASE'



}

// 后面的都是打包的配置
task sourcesJar(type: Jar) {
	archiveClassifier = 'sources'
	from sourceSets.main.allJava
}
// 生成 javadoc jar
task javadocJar(type: Jar) {
	archiveClassifier = 'javadoc'
	from javadoc.destinationDir
}
// javadoc 配置，这里是自定义了 java doc 的一些配置
javadoc {
	description = "Generates project-level javadoc for use in -javadoc jar"

	options.memberLevel = org.gradle.external.javadoc.JavadocMemberLevel.PROTECTED
	options.author = true
	options.version = true
	options.header = project.name
	options.addStringOption('Xdoclint:none', '-quiet')

	// suppress warnings due to cross-module @see and @link references;
	// note that global 'api' task does display all warnings.
	logging.captureStandardError LogLevel.INFO
	logging.captureStandardOutput LogLevel.INFO // suppress "## warnings" message
	options.encoding = "UTF-8"  //编码一定要配置否则直接出错
	options.charSet = 'UTF-8'
}

publishing {
	repositories {

		maven {
			name 'sonatypeRepository'
			url 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
			credentials {
				username = project.findProperty("sonatype.username")
				password = project.findProperty("sonatype.password")
			}
		}
		maven {
			name 'ALI_MAVEN'
			credentials {
				username project.findProperty("aliyun.username")
				password project.findProperty("aliyun.password")
			}
			url 'https://repo.rdc.aliyun.com/repository/78952-snapshot-kdIdFK/'
		}

		publications {
			mavenJava(MavenPublication) {
				groupId = group
				version = version
				from components.java
				artifact sourcesJar
				artifact javadocJar
				pom {
					name = "sunshine-common"
					description = "java tools depends on third party"
					url = "https://github.com/xiaodaojava/sunshine"
					licenses {
						license {
							name = "The Apache License, Version 2.0"
							url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
						}
					}
					developers {
						developer {
							id = "lixiang"
							name = "lixiang"
							email = "lixiang9409@vip.qq.com"
						}
					}
					scm {
						connection = "scm:git:https://github.com/xiaodaojava/sunshine.git"
						developerConnection = "scm:git:https://github.com/xiaodaojava/sunshine.git"
						url = "https://github.com/xiaodaojava/sunshine"
					}
				}

			}
		}
	}
}

signing {
	sign publishing.publications.mavenJava
}


